10 'Smooth scroll
20 LET[0],24,24,36,52,189,189,255,90
30 CLP:FOR J=0 TO 3:FOR I=0 TO 7
40 POKE#700+I+J*8,[I]>>(J*2)
50 POKE#720+I+J*8,[I]<<(8-J*2)
60 NEXT:NEXT
70 '
80 CLS:CLT:X=64:Y=15:A=0:B=0
90 @LOOP:IF TICK()<0 GOTO@BTN
100 Z=0
110 IF VPEEK(X/4,Y-1) Z=Z|#F0
120 IF VPEEK(X/4+1,Y-1) Z=Z|#0F
124 P=#F0>>(X%4)
125 C=P&Z
130 L=(C&128>0)+(C&64>0)+(C&32>0)+(C&16>0)
140 R=(C&8>0)+(C&4>0)+(C&2>0)+(C&1>0)
145 LC0,0:?L;"-";R;
150 IF L+R>=2 END
160 IF L+R=1 BEEP
170 CLT:POKE #900+RND(32),ASC("*"):SCROLL2
180 @BTN:B=BTN(RIGHT)-BTN(LEFT)
190 IF B A=A+B*(ABS(A)+1) ELSE IF A!=0 A=A/2
200 IF A<-8 A=-8 ELSE IF A>8 A=8
210 'GOSUB@DEL
220 X=X+A:IF X<4 X=4 ELSE IF X>124 X=124
230 GOSUB@POS
240 IF PEEK(Z)=0 POKE Z,224+X%4
250 IF PEEK(Z+1)=0 POKE Z+1,228+X%4
260 GOTO @LOOP
270 @POS:Z=#900+X/4+Y*#20:RTN
280 @DEL:GOSUB@POS:POKE Z,0,0:RTN
