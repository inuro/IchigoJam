10 CLS:LC3,10:?"HIGH SCORE:";H
20 IFINKEY()=0CONT
30 @BGN:LET[0],24,24,36,52,189,189,255,90
40 CLP:FORJ=0TO7:FORI=0TO7
50 POKE#700+I+J*8,[I]>>(J)
60 POKE#720+I+J*8,[I]<<(8-J)
70 NEXT:NEXT
80 POKE#800,16,180,8,32,0,2,250,48,8,24,2,136,67,136,17,70,210,8,91,1,211,24,6,48,3,68,10,70,7,33,10,64,255,36,
36,2,212,64,224,50,0,32,25,120,0,49,1,209,26,112,1,224,255,32,0,2,89,120,0,49,2,209
90 POKE#83E,8,50,90,112,0,224,255,48,32,64,16,188,112,71
100 CLS:CLT:X=127:Y=15:A=0:S=0:T=15
110 @LOOP:W=(TICK()/500)&T
120 IF RND(8)<W+1 LCRND(32),0:?"*";
130 IF RND(T)<W LCRND(28),0:?"■■■■";
140 SCROLL2
150 LC0,23:?"LV";W;"-SCORE:";S;
160 B=BTN(29)-BTN(28)
170 IF B=0 A=0 ELSEIF B*A<0 A=B ELSE A=A+B*(ABS(A)*3/2+1) 
180 I=ABS(A):IF I>14 A=14*A/I
190 I=X:X=X+A:IF X<4 X=4 ELSEIF X>124 X=124
200 Z=USR(#800,0)
210 D=(Z&#7E0>0)+(Z&#F81F>0)
220 IF D=1 BEEP:S=S+50
230 WAIT((T-W)/3):IFD<2 S=S+(ABS(I-X)+5)/6:GOTO@LOOP
240 CLK:PLAY"O3D24R24D8":LC10,10:IF S>H H=S:?"HIGH SCORE!";ELSE ?"GAME OVER";
250 IF INKEY()<>32 CONT ELSE GOTO@BGN

        
        10  タイトル。残り容量が少なくなるたびに削られる部分
        20  タイトルのキー入力待ち
        30  キャラの定義。1キャラ分8バイトx8ライン
        40  CLPしてるのはコンティニュー時にワンクッション置きつつPCG再定義の様子をビジュアライズするため
        50  1ループごとに1ドットづつ右にシフトしたデータを書き込む。この行は左側
        60  この行は右側。上とは逆に元のデータを左シフトしている
        70  
        80  マシン語部分の書き込み。内容はsmooth.asmを参照
        90  大雑把に言えばX、Yの位置への自機描画と、衝突を判定しフラグを立てたデータを返す
        100 ループ直前の変数初期化。X,Yが自機位置で、Xはこの場合仮想座標空間で0〜255。Yは通常通り0〜13。Aは現在の加速度、Sはスコア、Tは難易度の閾値
        110 現在のTICKを500で割ったものを難易度としている。Tで論理和を取って上限を設定
        120 RND(8)が難易度+1を下回った場合に障害物を描画する。つまり難易度が6を超えたら常時表示される
        130 それに加えてRND(T)が難易度Wを下回った場合に雲的な幅4の障害物を描画する
        140 障害物の描画を終えたら下にスクロール
        150 現在の難易度とスコアの表示。右下に表示し、次のスクロールですぐにワイプされる
        160 キー入力判定。BTN(29)はBTN(RIGHT)、あるいはPEEK(#1002)&2>0と等価。BTN(28)はBTN(LEFT)、あるいはPEEK(#1002)&1>0と等価
        170 B=0つまりキーが離されたら即座に停止。B*A<0つまり反対方向が入力されたら即座に反転。それ以外は二次曲線的に増加
        180 加速度の上限は14
        190 XにAを加算。Xの範囲は0〜124。これは2キャラ分描画になるため。加算前のXをIとして保持して後でスコアに使う
        200 マシン語のコール。自キャラの描画と衝突判定を行う。戻り値には、自キャラと障害物の衝突状態が2キャラ分16ビットのフラグでやってくる
        210 ダメージ判定。#7E0は0000011111100000であり、つまり中央部分で3ドット以内で交錯したか。#F81Fは逆に1111100000011111で5ドット以上判定
        220 ダメージが1ということは、3ドット以内でかすめてることになるのでボーナス
        230 ウェイトとスコア加算。スコアは左右に移動したときのみ加算されるようになっている。当たってなければループ
        240 ゲームオーバー。サウンドを鳴らし、ハイスコアを更新
        250 スペースキーの入力待ちで再ゲームへ
