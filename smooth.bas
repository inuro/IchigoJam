10 'Smooth scroll
20 LET[0],24,24,36,52,189,189,255,90
30 CLP:FOR J=0 TO 3:FOR I=0 TO 7
40 POKE#700+I+J*8,[I]>>(J*2)
50 POKE#720+I+J*8,[I]<<(8-J*2)
60 NEXT:NEXT
70 '
80 CLS:CLT:X=64:Y=15:A=0:B=0:S=0
90 @LOOP
100 Z=0
110 IF VPEEK(X/4,Y-1) Z=Z|#F0
120 IF VPEEK(X/4+1,Y-1) Z=Z|#0F
124 P=#F0>>(X%4)
125 C=P&Z
130 L=(C&128>0)+(C&64>0)+(C&32>0)+(C&16>0)
140 R=(C&8>0)+(C&4>0)+(C&2>0)+(C&1>0)
145 'LC0,0:?L;"-";R;
150 IF L+R>=2 END
160 S=S+1:IF L+R=1 BEEP:S=S+100
170 CLT:POKE #900+RND(32),ASC("*"):SCROLL2
175 LC0,23:?"SCORE:";S;
180 @BTN:B=(PEEK(#1002)&2>0)-(PEEK(#1002)&1>0)
190 IF A*B<0 A=B ELSE IF B=0 A=A/2 ELSE A=A+B*(ABS(A)+1) 
200 IF A<-8 A=-8 ELSE IF A>8 A=8
210 'GOSUB@DEL
220 X=X+A:IF X<4 X=4 ELSE IF X>124 X=124
230 GOSUB@POS
240 IF PEEK(Z)=0 POKE Z,224+X%4
250 IF PEEK(Z+1)=0 POKE Z+1,228+X%4
260 GOTO @LOOP
270 @POS:Z=#900+X/4+Y*#20:RTN
280 @DEL:GOSUB@POS:POKE Z,0,0:RTN
