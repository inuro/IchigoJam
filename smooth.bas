10 'Smooth scroll
20 LET[0],24,24,36,52,189,189,255,90
30 CLP:FOR J=0 TO 3:FOR I=0 TO 7
40 POKE#700+I+J*8,[I]>>(J*2)
50 POKE#720+I+J*8,[I]<<(8-J*2)
60 NEXT:NEXT
70 '
80 CLS:CLT:X=64:Y=15:A=0:B=0
90 @LOOP:IF TICK()<90 GOTO@BTN
96 IF VPEEK((X+1)/4,Y-1)||VPEEK((X+2)/4,Y-1) BEEP1:END
97 CLT:POKE #900+RND(32),ASC("*"):SCROLL2
100 @BTN:B=BTN(RIGHT)-BTN(LEFT)
110 IF B A=A+B ELSE IF A!=0 A=A-(A/ABS(A))
120 IF A<-3 A=-3 ELSE IF A>3 A=3
140 GOSUB@POS:POKE Z,0:POKE Z+1,0
180 X=X+A:IF X<4 X=4 ELSE IF X>124 X=124
190 GOSUB@POS
195 IF PEEK(Z)=0 POKE Z,224+X%4
200 IF PEEK(Z+1)=0 POKE Z+1,228+X%4
250 GOTO @LOOP
300 @POS:Z=#900+X/4+Y*#20:RTN
    
    
