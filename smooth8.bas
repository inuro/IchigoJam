10 'Smooth scroll
20 LET[0],24,24,36,52,189,189,255,90
30 CLP:FOR J=0 TO 7:FOR I=0 TO 7
40 POKE#700+I+J*8,[I]>>(J)
50 POKE#740+I+J*8,[I]<<(8-J)
60 NEXT:NEXT
70 '
80 CLS:CLT:X=64:Y=15:A=0:B=0
90 @LOOP:IF TICK()<0 GOTO@BTN
100 Z=0
110 IF VPEEK(X/8,Y-1) Z=#FF00
120 IF VPEEK(X/8+1,Y-1) Z=Z+#FF
125 CLT:POKE #900+RND(32),ASC("*"):SCROLL2
130 P=(#FF00>>(X%8))&Z
140 D=(P&#7E0>0)+(P&#F81F<>0)
170 LC0,23:?"SCORE:";S;" D:";D;
150 IF D=2 ?"D:";D:END ELSEIF D=1 BEEP:S=S+100 ELSE S=S+1
180 @BTN:B=BTN(RIGHT)-BTN(LEFT)
190 IF B A=A+B*(ABS(A)+1) ELSE IF A!=0 A=A/2
200 IF A<-12 A=-12 ELSE IF A>12 A=12
210 X=X+A:IF X<0 X=0 ELSE IF X>247 X=247
220 Z=#900+X/8+Y*32
230 IF PEEK(Z)=0 POKE Z,224+X%8
240 IF PEEK(Z+1)=0 POKE Z+1,232+X%8
250 GOTO @LOOP
